/**
 * File:
 *   scanner_new.ycp
 *
 * Module:
 *   Configuration scanner
 *
 * Summary:
 *   
 *
 * Authors:
 *   Gabriele Strattner <gs@suse.de>
 *
 * $Id$
 *
 * 
 * Installation of a new scanner. 
 * 
 *
 */

{
   textdomain "scanner";
   
  /**
   * ScannerStartDialog Layout	
   */
  
  global define ScannerNewDialog_UI( map new_scanner )``{

      string model  = "default_model";
      string vendor = "default_vendor";
      string driver = "default_driver";
      
      if ( new_scanner != $[] )
      {
	  model = lookup( new_scanner, "device", "" );
	  vendor = lookup( new_scanner, "vendor", "" );
	  driver = lookup( new_scanner, "scanner_driver", "" );
      }
      
      term layout = `VBox (
			   `VSpacing (1),
			   `Left(`Label (`opt (`hstretch), _("The scanner model"))),
			   `VSpacing(0.3),
			   `HBox (`HSpacing(2.5),
				  `HWeight(5, `Label (`opt (`outputField, `hstretch), model)),
				  `HWeight(1, `Empty())
				  ),
			   `VSpacing(0.5),
			   `Left(`Label (`opt (`hstretch), _("from vendor"))),
			   `VSpacing(0.3),
			   `HBox (`HSpacing(2.5),
				  `HWeight(5, `Label (`opt (`outputField, `hstretch), vendor)),
				  `HWeight(1, `Empty())
				  ),
			   `VSpacing(0.5),
			   `Left(`Label (`opt (`hstretch), _("uses the scanner driver"))),
			   `VSpacing(0.3),
			   `HBox (`HSpacing(2.5),
				  `HWeight(5, `Label (`opt (`outputField, `hstretch), driver)),
				  `HWeight(1, `Empty())
				  ),
			   `VSpacing (1),
			   `Left(`RadioButtonGroup (
						    `id (`action),
						    `VBox (
							   // radio button1: install the scanner found by YaST2
							   `RadioButton (`id (0), `opt (`hstretch), _("&Installation of this scanner"), true),
							   `VSpacing (1.0),
							   // radio button2: select another scanner
							   `RadioButton (`id (2), `opt (`hstretch), _("&Select a different scanner"))
							   )
						    )
				 ),
			   `VSpacing (3.0)
			   );
      return layout;

  };

  
/**
 * ScannerStartDialog	
 */
  global define ScannerNewDialog( )``{

      string help = "";
      map new_scanner = $[];
      
      // part 1 of helptext of dialog Scanner installation
      string help1    = UI(_("<p>YaST2 has detected a new scanner.
Please check the scanner data and go on
with installation if the data are <i>OK</i>. 
</p>"));
      // part 2 of helptext of dialog scanner installation
      string help2 = UI(_("<p>Click on <b>Next</b> to install
the scanner. YaST2 will set reasonable values for all scanner options.</p>"));

       // part 3 of helptext of dialog scanner installation
      string help3 =  UI(_("<p>If you want to install a different scanner
please click on the appropriate button.
YaST2 will show you then a list of all supported scanners.</p>"));
      
      help = help1 + help2 + help3;
      
      string caption = UI(_("Scanner installation"));

      y2debug( "New scanner: %1, %2", GetCurrentScannerValue( "vendor" ),
	       GetCurrentScannerValue( "device" ));

      term contents  = ScannerNewDialog_UI( GetCurrentScanner() );

      UI(`SetWizardContentsButtons(caption,contents,help,BackButtonLabel(),NextButtonLabel()));

      // TODO: *do* the scanner configuration
      // if ( configure_ok )

      SetCurrentScannerValue( "scanner_status", "installed" );
      
      any ret = UI(`UserInput());


      return ret;
      
  };

}
