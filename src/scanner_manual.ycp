/**
 * File:
 *   scanner_manual.ycp
 *
 * Module:
 *   Configuration of scanner
 *
 * Summary:
 *   
 *
 * Authors:
 *   Gabriele Strattner <gs@suse.de>
 *
 * String corrections by Christian Steinruecken <cstein@suse.de>, 2001/07/28
 *
 * $Id$
 *
 * 
 * Manual scanner selection. 
 * 
 *
 */

{
  textdomain "scanner";

  global define CreateVendorList( ) ``{
      list list_input = [];
      list vlist = SCR(`Read(.sane.vendorlist, GetCurrentScanner()));
      y2debug( "Vendorlist: %1", vlist );
      
      foreach( `vendor, vlist, ``{
		  list_input = add( list_input, `item(`id(vendor), vendor));
	      });

      return list_input; 
  }

  global define CreateModelList( string vendor ) ``{
      list list_input = [];
      SetCurrentScannerValue( "vendor", vendor );
      list mlist = SCR(`Read(.sane.modellist, GetCurrentScanner()));
      y2debug( "Modellist: %1", mlist );

      foreach( `model, mlist, ``{
		  list_input = add( list_input, `item(`id(model), model));
	      });

      return list_input; 
  }
  
  global define ScannerManualDialog_UI( map current_scanner ) ``{

      string vstring =  lookup( current_scanner, "vendor", "" );
      
      if ( vstring == "" )
      {
	  vstring = lookup( current_scanner, "bus", "" ) + " " + _("scanner");
      }
      string mstring = lookup( current_scanner, "device", "" );
      if ( mstring == "" )
      {
	  mstring = lookup( current_scanner, "dev_name", "" );
      }
      
      string scanner = vstring + " - " + mstring;

      list vendor_list = CreateVendorList();
      list model_list  = CreateModelList( select( select(vendor_list, 0), 1) );

      term layout = `VBox(
			  `VSpacing (1),
			  // this is a label; the name of the scanner follows
			  `Left(`Label (`opt (`hstretch), _("The scanner"))),
			  `VSpacing(0.3),
			  `HBox (`HSpacing(2.5),
				 `HWeight(5, `Label (`opt (`outputField, `hstretch), scanner)),
				 `HWeight(1, `Empty())
				 ),
			  `VSpacing(0.6),
			  // this is a headline; the lists with vendors and corresponding models follows  
			  `Left( `Label (`opt (`hstretch), _("will be installed as:")) ),
			  `VSpacing (0.3),
			  `HBox(`HSpacing(0.5),
				`HBox (`SelectionBox (`id (`vendor), `opt (`notify),
						      _("&Select vendor"), vendor_list ),
				       `ReplacePoint (`id (`model_rep),`SelectionBox (`id (`model), `opt (`notify),
										      // selection box label
										      _("Select &model"), model_list))),
				`HSpacing(3.0)
				),
			  `VSpacing(1.0)
			  );
      return layout;
  }

/**
 * ScannerManualDialog
 */
  global define ScannerManualDialog()``{
      string help	= "";
      // help text1 for dialog Manual scanner selection
      // - don't translate SANE (Scanner Access Now Easy) - 
      string help1    	= UI(_("<p>This dialog shows all scanners which are supported
by the SANE library (Scanner Access Now Easy).</p> "));
      // help text2 for dialog Manual scanner selection
      string help2    	= UI(_("<p>If one of the vendor/model pairs matches
your scanner, select the corresponding entries and proceed with the installation.</p>"));
      // helptext3 for dialog Manual scanner selection
      string help3    	= UI(_("<p><b>Attention</b>: only do a manual
selection if you are absolutely sure. Wrong data may cause hardware
failures.</p>"));
      // help text4 for dialog Manual scanner selection
      // - don't translate SANE -
      string help4    	= UI(_("<p>If your scanner model is not
contained in the list but you know a SANE driver which works for your scanner,
take \"Generic\" in the vendor list and select the driver manually.</p>"));
      // helptext5 for dialog Manual scanner selection
      string help5    	= UI(_("<p>More information can be found on
http://mostang.com/sane and http://sdb.suse.de.</p>"));
      help = help1 + help2 + help3 + help4 + help5;

      string caption = UI(_("Manual scanner selection"));
      term contents  = ScannerManualDialog_UI( GetCurrentScanner() );

      UI(`SetWizardContentsButtons(caption,contents,help,BackButtonLabel(),NextButtonLabel()));

      any ret = nil;
      string vendor = "";
      string model = "";
      
      repeat {
	  ret = UI(`UserInput());

	  if ( ret == `vendor )
	  {
	      vendor = UI(`QueryWidget(`id(`vendor), `CurrentItem));

	      UI(`ReplaceWidget(`id(`model_rep), `SelectionBox (`id (`model),
								`opt (`notify),
								// selection box label
								_("Select &model:"), CreateModelList(vendor))));
	  }
	  else if ( ret == `next )
	  {
	      vendor = UI( `QueryWidget( `id(`vendor), `CurrentItem) );
	      model  = UI( `QueryWidget( `id(`model ), `CurrentItem) );

	      if ( vendor != nil && model != nil )
	      {
		  SetCurrentScannerValue( "vendor", vendor );
		  SetCurrentScannerValue( "device", model );
	      }
	      else
	      {
		  UI(`NotifyPopup(_("Please select a vendor AND a model from the lists.")));
		  ret = `again; 
	      }
	  }

	  y2milestone("MANUAL scanner selection: %1 - %2", GetCurrentScannerValue("vendor"),
		      GetCurrentScannerValue("device"));
	  
      
      } until ( ret == `next || ret == `back || ret == `abort );

      return ret;
      
  } 	// end ScannerManualDialog()
      
}
