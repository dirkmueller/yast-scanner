/**
 * File:	include/scanner/dialog.ycp
 * Package:	Configuration of scanner
 * Summary:	Main dialogs definition
 * Authors:	Gabriele Strattner <gs@suse.de>
 *
 * $Id$
 */

{

/***
 * <h3>Scanner Cconfiguration</h3>
 */

textdomain "scanner";

import "Wizard";
import "Popup";
import "Label";
import "Scanner";

include "scanner/scanner_new.ycp";
include "scanner/scanner_overview.ycp";
include "scanner/scanner_install.ycp";
include "scanner/scanner_notfound.ycp";
include "scanner/scanner_scsi.ycp";
include "scanner/scanner_usb.ycp";
include "scanner/scanner_net.ycp";
include "scanner/scanner_manual.ycp";

/**
 * Read settings dialog
 * @return `abort if aborted and `next otherwise
 */
define symbol ReadDialog() {
    y2milestone ("Running read dialog");
    // Help text for start dialog (checking and probing)
    string help_text = _("<p>YaST2 is doing some checks, such as the
hardware probing of 'USB' or 'SCSI' ports.
</p>
");
    Wizard::RestoreHelp(help_text);
    // PowerManagement::AbortFunction = PollAbort;
    boolean ret = Scanner::Read();
    return ret ? `next : `abort;
}

/**
 * Write settings dialog
 * @return `abort if aborted and `next otherwise
 */
define symbol WriteDialog () {

    /* info text shown at the end of module */
    string message = _("Please wait while the configuration files are updated...");
    UI::OpenDialog(`opt(`decorated), `Label(message));

    y2milestone("FINISH the scanner installation - confirm all changes." );

    boolean ret = Scanner::Write(lookup(Scanner::user_settings, "scannerInfo", $[]));
    return ret ? `next : `abort;
}

/**
 * Abort dialog
 */
define symbol AbortDialog() ``{
    boolean ret = Popup::ReallyAbort(true);
    if ( ret==true )
    {
	y2milestone("ABORT the scanner installation - undo all changes.");
	boolean ok = (boolean) SCR::Execute(.sane.revertall);
	if(!ok)
	{
	    Popup::Message(_("It was not possible to revert the already made changes"));
	}
	return `yes;
    }
    return `back;
}



/* EOF */
}
