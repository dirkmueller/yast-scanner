/**
 * File:
 *   scanner_start.ycp
 *
 * Module:
 *   Configuration of scanner
 *
 * Summary:
 *   
 *
 * Authors:
 *   Gabriele Strattner <gs@suse.de>
 *
 * String corrections by Christian Steinruecken <cstein@suse.de>, 2001/07/28
 *
 *
 * $Id$
 *
 * 0. is package "sane" installed?
 *    if NOT -> call sw_single
 * 1. Hardware probing
 *    1a) USB: SCR(.probe.scanner)	
 *    1b) SCSI: sane-find-scanner
 * 2. Check already installed scanner (perl script)
 * 3. Decide whether the detected scanner(s) is (are) installed/supported by SANE
 * 4. Decide which dialog is called next
 * 
 * Shows the progress of hardware probing and scanner checking. 
 *
 */

{
  textdomain "scanner";
  include "ui/progress.ycp";


/**
 * ScannerStartDialog
 */
  global define ScannerStartDialog( )``{

      any ret = nil;
      boolean test_mode = lookup( user_settings, "test_mode", false );

      // Help text for start dialog (checking and probing)
      string help_text = UI(_("<p>YaST2 is doing some checks, e.g. the 
hardware probing of 'USB' and/or the 'SCSI' port(s).
</p>"));
	
      UI(`ProgressSetupUI(
			  // Headline for start dialog of scanner configuration
			  _("Initializing scanner module ..."),
			  "",	// progress_title
			  1000,	// length
			  [
                           // Translators: Please do not translate 'SANE'!
			   _("Checking whether SANE library is installed..."),
			   _("Probing hardware - looking for connected scanners..."),
			   _("Checking already installed scanners...")
			  ],
			  help_text )
	 );

      UI(`ProgressMilestoneUI( 100, _("Checking SANE library..."), 0 ));
      //sleep(100);
    
      if ( !test_mode )
      {
	  // check whether 'sane' is installed
	  if ( SCR (`Execute (.target.bash, "/bin/rpm -q sane")) != 0 )
	  {
	      // Translators: 'SANE' is a package name - don't translate!
	      string mesg = _("For scanner access under Linux, the SANE library is required.
Do you want to install the 'SANE' package now?");
	      boolean cont = UI ( `ContinueCancelPopup(mesg) );
	      if ( cont )
	      {
		  string tmpdir = SCR (`Read (.target.tmpdir));
		  string filename = tmpdir + "/sw_single_input";
		  SCR (`Write (.target.ycp, filename, $["install":["sane"], "userInput" : false ]));
		  CallFunction (`sw_single (filename));
	      }
	      else
	      {
		  return `cancel;
	      }
	  }

	  // TODO: check version of sane package (use PKGINFO rpm agent)
	  // SANE should be installed by now - if not inform the user and exit
	  if ( SCR (`Execute (.target.bash, "/bin/rpm -q sane")) != 0 )
	  {
              // Translators: Please do not translate 'SANE'!
	      UI( `ErrorPopup(_("The package 'SANE' is not installed correctly.
This package is mandatory for the scanner installation.
Please make sure that you have the SuSE CD set available
and restart the scanner installation - or install the package
with the YaST2 module 'Install/Remove Software'.\n")));
	      return `cancel;
	  }
      }
    
      // Progress bar (1)
      UI(`ProgressMilestoneUI( 200, _("Probing hardware"), 1 ));
      //sleep(100);
      
      list connected_scanner = [];
      if ( !test_mode )
      {
	  // probe USB ports; command is hwinfo --scanner
	  connected_scanner = SCR(`Read(.probe.scanner));
	  
	  if ( size(connected_scanner) > 1 ) 
	  {
	      // only one USB scanner is supported by scanner library SANE
	      // and there are problems with hot plugin (changing device numbers)
	      // -> only *one* USB scanner is allowed

	      // text of an error popup "only one USB scanner allowed" 
	      UI(`ErrorPopup(_("YaST2 has detected several USB scanners
connected to your computer. At the moment only one USB scanner\n
can be configured, so please decide which one you want.\n
Disconnect all unwanted USB scanners and restart the scanner\n
configuration.")));
	      return `cancel;
	    
	  }
	  else if ( size(connected_scanner) == 1 )
	  {
	      // add the devicefile - rebuild the list
	      map usb_scanner = select( connected_scanner, 0 );
	      connected_scanner = [];
	      usb_scanner = add( usb_scanner, "dev_name", "/dev/usbscanner" );
	      connected_scanner = add( connected_scanner, usb_scanner );
	  }
	  y2milestone( "USB Scanner: %1", connected_scanner);
          // check SCSI ports
	  list scsi_scanner = SCR(`Read(.scanner.scsi));
	  connected_scanner = union(connected_scanner, scsi_scanner);
	  y2milestone( "SCSI Scanner: %1", scsi_scanner );
      }
      else
      {
	  connected_scanner = lookup( user_settings, "test_connected", [] );
      }

      UI(`ProgressMilestoneUI( 300, _("Checking already installed scanners"), 2 ));
      sleep(100);

      list installed_scanner = [];
      if ( !test_mode )
      {
	  installed_scanner = SCR(`Read(.scanner.configured));
	  y2debug("Configured scanner: %1", installed_scanner );
      }
      else
      {
	  installed_scanner = lookup( user_settings, "test_installed", [] );	
      }
      
      UI(`ProgressMilestoneUI( 1000, _("Finishing the check procedure"), 2 ));
      sleep(500);

      // find out which scanner is already installed / which is new / which is supported
      map scanner_info = $[];
    
      integer no = 1;
      
      foreach ( `scanner, connected_scanner, ``{
	  string driver = "";
	  if ( !test_mode )
	  {
	      // try to get SANE scanner driver
	      driver = SCR(`Read(.sane.driver, scanner));
	  }
	  else
	  {
	      // get test data
	      driver = lookup( scanner, "scanner_driver", "" ); 
	  }
	  if ( installed_scanner == [] )
	  {
	      if ( driver != "" )
	      {
		  scanner = add( scanner, "scanner_status", "supported" );
		  scanner = add( scanner, "scanner_driver", driver );
	      }
	      else
	      {
		  scanner = add( scanner, "scanner_status", "detected" ); 
	      }
	  }
	  else
	  {
	      foreach ( `scan, installed_scanner, ``{
		  if ( (lookup( scan, "vendor", "") == lookup( scanner, "vendor", "")) &&
		       (lookup( scan, "device", "") == lookup( scanner, "device", "")) &&
		       (lookup( scan, "bus", "") )  == lookup( scanner, "bus", "") &&
		       (lookup( scan, "dev_name", "") == lookup( scan, "dev_name", "")) )
		  {
		      scanner = add( scanner, "scanner_status", "installed" );
		  }
		  else if ( driver != "" )
		  {
		      scanner = add( scanner, "scanner_status", "supported" );
		      scanner = add( scanner, "scanner_driver", driver );
		  }
		  else
		  {
		      scanner = add( scanner, "scanner_status", "detected" );  
		  }
	      });
	  }
	  scanner_info = add(scanner_info, no, scanner);
	  no = no+1;
      });

      // default: go next to Overview Dialog
      ret = `next;
      
      // write scanner data to user_settings
      user_settings = add( user_settings, "scannerInfo", scanner_info );
      y2debug( "Writing SCANNER INFO: %1", scanner_info );

      // decide which dialog comes next
      if ( !lookup( user_settings, "check_again", false ))
      {
	  ret = DecideDialog( `start );
      }
      else
      {
	  ret = DecideDialog( `add );
      }

      return ret;
  }

}
