/**
 * File:
 *   scanner_start.ycp
 *
 * Module:
 *   Configuration of scanner
 *
 * Summary:
 *   
 *
 * Authors:
 *   Gabriele Strattner <gs@suse.de>
 *
 * $Id$
 *
 * 0. is package "sane" installed?
 *    if NOT -> call sw_single
 * 1. Hardware probing
 *    1a) .probe.scanner (USB)	
 *    1b)  SCSI: sane-find-scanner
 *         if (scsi_scanner_found)
 *		inform user that a script is started at boot time
 *			if (user_yes)
 *				install boot script (check/reset permissions of /dev/sg_)
 *			else
 *				exit
 *    1c)	set permission of /dev/sg_ to 666
 * 2. Check already installed scanner (perl script)
 * 3. Decide whether the detected scanner(s) is (are) installed
 * 4. Decide which dialog is called next
 *    - at least one new scanner detected/no scanner configured
 *      -> go to dialog scanner_new (show first detected scanner)
 *    - at least one new scanner detected/at least one scanner configured
 *	-> show dialog scanner_overview AND a popup
 *	   "New scanner - configuration wanted?"
 *    - nothing new, but at least one scanner configured
 *      -> show dialog scanner_overview
 *    - nothing detected at all
 *	-> go to dialog scanner_notfound
 * 
 * Shows the progress of hardware probing and scanner checking. 
 * 
 *
 */

{
  textdomain "general";
  include "ui/progress.ycp";


/**
 * ScannerStartDialog
 */
  global define ScannerStartDialog( )``{

      any ret = nil;
      boolean test_mode = lookup( user_settings, "test_mode", false );

      
      // Help text for last dialog of base installation
      string help_text = UI(_("<p>
YaST2 starts the hardware probing, i.e. checks the 'USB' and/or
the 'SCSI' port(s) for connected scanner(s).
If a new scanner is detected the next dialog allows you to
configure easily the scanner.
If the automatic detection fails you have the possibility to continue
with manual installation in following dialogs.
</p>"));
	

      UI(`ProgressSetupUI(
			  // Headline for start dialog of scanner configuration
			  _("Initialising scanner module ..."),
			  "",	// progress_title
			  1000,	// length
			  [
			   _("Checking whether SANE library is installed"),
			   _("Hardware probing - looking for connected scanner(s)"),
			   _("Checking for already installed scanner")
			  ],
			  help_text )
	 );

      UI(`ProgressMilestoneUI( 100, _("Checking for SANE"), 0 ));
      sleep(1000);
    
      if ( !test_mode )
      {
	  // check whether 'sane' is installed
	  if ( SCR (`Execute (.target.bash, "/bin/rpm -q sane")) != 0 )
	  {
	      string tmpdir = SCR (`Read (.target.tmpdir));
	      string filename = tmpdir + "/sw_single_input";
	      SCR (`Write (.target.ycp, filename, $["install":["sane"], "userInput" : false ]));
	      CallFunction (`sw_single (filename));
	  }

	  // SANE should be installed by now - if not notify the user and exit
	  if ( SCR (`Execute (.target.bash, "/bin/rpm -q sane")) != 0 )
	  {
	      UI( `ErrorPopup(_("Package 'sane' is not installed correctly.
This package is mandatory for the scanner installation.
Please make sure that you have the SuSE CD set available
and start scanner installation again - or install the package
with YaST2 module 'Install/Remove Software'.\n")));

	      return `cancel;
	  }
      }
    
      UI(`ProgressMilestoneUI( 200, _("Hardware probing"), 1 ));
      sleep(100);

      list connected_scanner = [];
    
      if ( !test_mode )
      {
	  // probe USB ports
	  connected_scanner = SCR(`Read(.probe.scanner));

	  if ( size(connected_scanner) > 1 ) 
	  {
	      // only one USB scanner is supported by scanner library SANE
	      // and there are problems with hot plugin (changing device numbers)
	      // -> only *one* USB scanner is allowed
	      UI(`ErrorPopup("YaST2 has detected several USB scanners
connected to your PC. At the moment it's only
possible to support ONE single USB scanner.\n
Please disconnect the unwanted USB scanner(s)
and restart the scanner configuration."));
	      return `cancel;
	    
	  }
	  // check SCSI ports
	  // connected_scanner = add(connected_scanner, SCR(`Execute(.target.bash, "get_scsi_scanner" )));
	  y2debug("Connected Scanner: %1", connected_scanner);
      }
      else
      {
	  connected_scanner = lookup( user_settings, "test_connected", [] );
      }

    
      UI(`ProgressMilestoneUI( 300, _("Checking for already installed scanner"), 2 ));
      sleep(100);

      list installed_scanner = [];
    
      if ( !test_mode )
      {
	
	  //list installed_scanner = SCR(`Execute(.target.bash, "get_configured_scanner" ));
      }
      else
      {
	  installed_scanner = lookup( user_settings, "test_installed", [] );	
      }

      UI(`ProgressMilestoneUI( 1000, _("Checking for already installed scanner"), 2 ));
      sleep(500);

      // find out which scanner is already installed/which is new

      map scanner_info = $[];
      boolean installed = false;
      boolean new = false;
    
      integer no = 1;
      foreach( `scanner, connected_scanner, ``{
	  foreach ( `scan, installed_scanner, ``{
	      if ( (lookup( scan, "vendor", "") == lookup( scanner, "vendor", "")) &&
		   (lookup( scan, "device", "") == lookup( scanner, "device", "")) &&
		   (lookup( scan, "bus", "") )  == lookup( scanner, "bus", "") &&
		   (lookup( scan, "devicefile", "") == lookup( scan, "devicefile", "")) )
	      {
		  scanner_info = add(scanner_info, no, add( scan, "new_scanner", false));
		  installed = true;
	      }
	      else
	      {
		  scanner_info = add(scanner_info, no, add( scan, "new_scanner", true));
		  new = true;
	      }
	  });
	  no = no+1;
      });
      if ( new )
      {
	  ret = `new;
      }
      else if ( installed )
      {
	  ret = `overview;
      }
      else
      {
	  ret = `manual;
      }

      if ( scanner_info != $[] )
      {
	  // write scanner data to user_settings
	  user_settings = add( user_settings, "scannerInfo", scanner_info );
      }

      return ret;
  }

}
