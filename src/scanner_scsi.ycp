/**
 * File:
 *   scanner_scsi.ycp
 *
 * Module:
 *   Configuration scanner
 *
 * Summary:
 *
 * This dialog is shown very rarely because the SANE library usually finds
 * all connected SCSI scanner.
 * Only for some (don't no exactly which reasons) is can be useful to
 * enter the SCSI device manually.
 * For security reasons we show a ComboBox containig only /dev/sg. entries
 * (not an inputfield for free text).
 *
 * Authors:
 *   Gabriele Strattner <gs@suse.de>
 *
 * $Id$
 *
 * String corrections by Christian Steinruecken <cstein@suse.de>, 2001/07/28
 * 
 * 
 *
 */

{

  textdomain "scanner";
  
  /**
   * ScannerSCSIDialog Layout	
   */
  
  global define ScannerSCSIDialog_UI( )``{

      list scsi_devices = [ "/dev/sg0", "/dev/sg1", "/dev/sg2", "/dev/sg3", "/dev/sg4", "/dev/sg5",
			  "/dev/sg6", "/dev/sg7", "/dev/sg8", "/dev/sg9", "/dev/sg10", "/dev/sg12" ];
      
      term layout = `VBox (
			   // This is a label, i.e. the line length should not exceed 50 characters.
			   `Left(`Label (`opt (`hstretch), _("You must supply the device file name before
the scanner installation can proceed.\n
Please select the name of the SCSI device, e.g. /dev/sg0.\n"))),
			   `VSpacing (1),
			   `HBox (
				  // SCSI device input 
				  `HWeight(4, `ComboBox(`id(`device),
							 // Combo box label for device file selection
							 _("&SCSI device file"),
							 scsi_devices )),
				  `HWeight(1, `Empty())
				  ),
			   `VSpacing (1)
			   );


      return layout;

  };

  
/**
 * ScannerSCSIDialog	
 */
  global define ScannerSCSIDialog( )``{

      string help = "";
      
      // helptext of dialog SCSI scanner configuration
      string help1    = UI(_("<p>Please select the device file
name of your SCSI scanner (e.g. /dev/sg0 ).
</p>"));

      // helptext of dialog SCSI scanner configuration (continued)
      string help2 = UI(_("<p>Information on your SCSI devices can be obtained
by issuing the command <b>hwinfo --scsi</b>
(you must be 'root' to do this).</p>"));

      // helptext of dialog SCSI scanner configuration (continued)
      // Translators: do not translate 'Device File'
      string help3 = UI(_("<p>You will find the <i>'Device File'</i> of your scanner in the
respective section of the hwinfo output.</p>"));

      help = help1 + help2 + help3;
      
      string caption = UI(_("Manual SCSI Scanner configuration"));

      term contents  = ScannerSCSIDialog_UI( );

      UI(`SetWizardContentsButtons(caption,contents,help,BackButtonLabel(),NextButtonLabel()));

      any ret = nil;

      repeat {
	  
	  ret = UI(`UserInput());

	  if ( ret == `next )
	  {
	      string devicefile = CutBlanks( UI(`QueryWidget(`id(`device), `Value )) );

	      // Create the entry for the SCSI scanner
	      CreateScannerEntry( $["bus":"SCSI", "devicefile":devicefile] ); 
	  }
	  
      } until ( ret == `next || ret == `back || ret == `abort );
      
      return ret;
  };

}
