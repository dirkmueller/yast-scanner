/**
 * File:
 *   scanner_overview.ycp
 *
 * Module:
 *   Configuration of scanner
 *
 * Summary:
 *   This dialog is shown at the begin of the workflow, if an already configured
 *   scanner is found AND at the end when a new scanner is configured.
 *   Leaving the dialog with Finish means: confirm the installation, Abort means
 *   reset all changes.
 *
 *  	if (user_click_on_finish)
 *	   - let all changes made on system
 *	   - maybe additional changes necessary?
 *         - if (scsi_scanner_installed)
 *		install boot script (check/reset permissions of /dev/sg_)
 *    		(because permissions of /dev/sg_ are set to 666)   
 *
 *	if (user_click_on_abort)
 *	   - reset all changes
 *
 * Authors:
 *   Gabriele Strattner <gs@suse.de>
 *
 * $Id$
 *
 *
 */

{

  testdomain "scanner";
  
  global define ScannerOverviewDialog_UI( list table_contents ) ``{

      term layout = `Empty();

      layout = `VBox(
		     `VSpacing( 0.2 ),
		     `Table(`id(`scanner_table),`opt(`notify, `hvstretch), 
			    `header( _("Vendor"), _("Model"), _("Bus") ),
			    table_contents
			    ),
		     `VSpacing(2.0),
		     `HBox(
			   `HWeight(1, `PushButton(`id(`edit), _("&Edit"))),
			   `HWeight(1, `PushButton(`id(`delete), _("&Delete"))),
			   `HWeight(1, `PushButton(`id(`add), `opt(`default), _("&Add"))),
			   `HStretch()
			   ),
		     `VSpacing( 0.2 )
		     );

      return layout;
  };

  /**
   * OverviewDialog dialog
   */
   
  global define ScannerOverviewDialog()``{
      map  scanner_map 	= lookup( user_settings, "scannerInfo", $[] );
      list scanner_table = [];
      
      string help = "";
      
      string help1    = UI(_("<p>The overview dialog shows the
configured  scanner(s).
Click on button <b>Edit</b> if you want to change a scanner
configuration.</p>"
			     ));
      string help2 = UI(_("<p>
You also can uninstall a scanner, select the list entry and
choose <b>Delete</b>. If you want to install a new scanner
click on <b>Add</b> button. 
</p>"));
      string help3 = UI(_("<p>
Press button <b>Finish</b> to save all scanner settings
and exit the scanner configuration workflow.
If you leave this dialog with <b>Abort</b> all changes
made on your system will be reseted.
</p>"));
      help = help1 + help2 + help3;
      
      foreach (`key, `value, scanner_map, ``{
	  if ( lookup(value, "scanner_status", `emtpy) == `installed )
	  {
	      scanner_table = add(scanner_table,
				  `item(`id(key),
					lookup(value, "vendor", ""),
					lookup(value, "device", ""),
					lookup(value, "bus", "")));
	  }
      });
	       
      string caption = UI(_("Scanner Overview"));

      term contents = ScannerOverviewDialog_UI( scanner_table );

      UI(`SetWizardContentsButtons(caption,contents,help,
				   BackButtonLabel(),
				   NextButtonLabel()));

      term finish_button = `PushButton(`id(`finish), FinishButtonLabel() );
      UI(`ReplaceWizardNextButton( finish_button ));
      
      any ret = nil;
      
      repeat {
	  integer scanner_id = 0;
	  boolean go_on = true;
	   
	  ret = UI(`UserInput());

	  if ( ret == `finish || ret == `delete || ret == `edit )
	  {
	      // which scanner is selected?
	       scanner_id = UI(`QueryWidget( `id(`scanner_table), `CurrentItem ));
	       y2debug( "IIIDDD: %1", scanner_id );
	       if ( scanner_id == nil )
	       {
		   UI(`NotifyPopup(_("Please select a scanner in list.")));
		   ret = `again;
		   go_on = false;
	       }
	       else
	       {
		   user_settings = add(user_settings, "scannerInWork", scanner_id);
		   y2debug( "SELECTED SCANNER: %1", GetCurrentScannerValue("device") );
	       }
	  }

	  if ( go_on )
	  {
	      if ( ret == `delete )
	      {
		  string message = sformat( UI(_("Do you really want to remove the
configuration of the scanner
%1, %2 ?\n")), GetCurrentScannerValue("vendor"),GetCurrentScannerValue("device") );   

		  UI(`YesNoHeadlinePopup( WarningMsg(), message) );
	      }
	      else if ( ret == `edit )
	      {
	      
	      }
	  }

      } until ( ret == `finish || ret == `abort || ret == `back || ret == `add );

      // restore Next button
      term next_button = `PushButton(`id(`next), NextButtonLabel() );
      UI(`ReplaceWizardNextButton( next_button ));

      return ret;
  }

}
