/**
 * File:
 *   scanner_overview.ycp
 *
 * Module:
 *   Configuration of scanner
 *
 * Summary:
 *   This dialog is shown at the begin of the workflow, if an already configured
 *   scanner is found AND at the end when a new scanner is configured.
 *   Leaving the dialog with Finish means: confirm the installation, Abort means
 *   reset all changes.
 *
 *  	if (user_click_on_finish)
 *	   - let all changes made on system
 *	   - maybe additional changes necessary?
 *         - if (scsi_scanner_installed)
 *		install boot script (check/reset permissions of /dev/sg_)
 *    		(because permissions of /dev/sg_ are set to 666)   
 *
 *	if (user_click_on_abort)
 *	   - reset all changes
 *
 * Authors:
 *   Gabriele Strattner <gs@suse.de>
 *
 * String corrections by Christian Steinruecken <cstein@suse.de>, 2001/07/27
 *
 * $Id$
 *
 *
 */

{

  textdomain "scanner";
  
  global define ScannerOverviewDialog_UI( list table_contents ) ``{

      term layout = `Empty();

      layout = `VBox(
		     `VSpacing( 0.2 ),
		     `Left(`Label(_("List of installed / configured scanners"))),
		     `VSpacing( 0.2 ),
		     `Table(`id(`scanner_table),`opt(`notify, `hvstretch), 
			    `header( _("Vendor"), _("Model"), _("Connected to") ),
			    table_contents
			    ),
		     `VSpacing(2.0),
		     `HBox(
			   //`HWeight(1, `PushButton(`id(`edit), _("&Edit"))),
			   `HWeight(1, `PushButton(`id(`delete), _("&Delete"))),
			   `HWeight(1, `PushButton(`id(`add), `opt(`default), _("&Add"))),
			   `HStretch()
			   ),
		     `VSpacing( 0.2 )
		     );

      return layout;
  };

  /**
   * OverviewDialog dialog
   */
   
  global define ScannerOverviewDialog()``{
      map  scanner_map 	= lookup( user_settings, "scannerInfo", $[] );
      list scanner_table = [];
      
      string help = "";
      // help part1 of scanner overview dialog
      string help1    = UI(_("<p>This overview dialog shows the
 currently configured and/or installed scanners.</p>"
			     ));
       // help part2 of scanner overview dialog
      string help2 = UI(_("<p>
If you want to remove a scanner configuration,
select the corresponding entry from the list and press <b>Delete</b>.
If you want to install a new scanner,
press the <b>Add</b> button.
</p>"));
       // help part3 of scanner overview dialog
      string help3 = UI(_("<p>Press the <b>Finish</b> button to save the
new configuration.</p>"));
      // help part4 of scanner overview dialog
      string help4 = UI(_("<p>
If you leave this dialog with <b>Abort</b>, all changes made in this dialog
will be discarded and your system will be reverted to the previous state.
</p>"));
      help = help1 + help2 + help3 + help4;
      
      foreach (`key, `value, scanner_map, ``{
	  if ( lookup(value, "scanner_status", `emtpy) == "installed" )
	  {
	      scanner_table = add(scanner_table,
				  `item(`id(key),
					lookup(value, "vendor", ""),
					lookup(value, "device", ""),
					lookup(value, "bus", "")));
	  }
      });
	       
      string caption = UI(_("Scanner Overview"));

      term contents = ScannerOverviewDialog_UI( scanner_table );

      UI(`SetWizardContentsButtons(caption,contents,help,
				   BackButtonLabel(),
				   NextButtonLabel()));

      term finish_button = `PushButton(`id(`finish), FinishButtonLabel() );
      UI(`ReplaceWizardNextButton( finish_button ));
      
      any ret = nil;
      boolean go_on = true;

      while (go_on )
      {
	  integer scanner_id = 0;

	  ret = UI(`UserInput());

	  if ( ret == `delete )
	  {
	      // which scanner is selected?
	      scanner_id = UI(`QueryWidget( `id(`scanner_table), `CurrentItem ));
	      if ( scanner_id == nil )
	      {
		  UI(`NotifyPopup(_("Please select a scanner from the list.")));
	      }
	      else
	      {
		  user_settings = add(user_settings, "scannerInWork", scanner_id);
		  y2debug( "DELETE scanner: %1", GetCurrentScannerValue("device") );

		  string message = sformat( UI(_("Do you really want to remove the
configuration of the scanner %1, %2 ?\n")), GetCurrentScannerValue("vendor"),
					    GetCurrentScannerValue("device") );   

		  UI(`YesNoHeadlinePopup( WarningMsg(), message) );
		  
	      }
	  }
	  else if ( ret == `finish )
	  {
	      // TODO: is there anything to do ???? 
	      any ret = UI(`YesNoPopup(_("Save all settings and exit?")));
	      if ( ret )
	      {
		  y2debug( "FINISH scanner workflow" );
		  go_on = false;
	      }
	  }
	  else if ( ret == `add )
	  {
	      ret = DecideDialog( `add );
	      go_on = false;
	  }
	  else
	  {
	      go_on = false;
	  }
	  
      }

      // restore Next button
      term next_button = `PushButton(`id(`next), NextButtonLabel() );
      UI(`ReplaceWizardNextButton( next_button ));

      return ret;
  }

}
