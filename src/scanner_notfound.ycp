/**
 * File:
 *   scanner_notfound.ycp
 *
 * Module:
 *   Configuration of scanner
 *
 * Summary:
 *   
 *
 * Authors:
 *   Gabriele Strattner <gs@suse.de>
 *
 * $Id$
 *
 * 
 * Notfound dialog. 
 * 
 *
 */

{
  textdomain "scanner";

  global define ScannerNotFoundDialog_UI() ``{

      term layout = `Empty();

      layout = `VBox( `Label(`opt (`hstretch), _("Your scanner was not detected automatically -
a manual configuration is necessary.")),
		      `VSpacing(1.5),
		      `HBox(`HSpacing(0.5),
			    `Frame (_("Select the scanner type"),
				    `RadioButtonGroup(`id(`scanner),
						      `VBox(`VSpacing (0.6),
							    `Left(`RadioButton(`id(`usb),`opt (`hstretch),
									       _("&USB scanner"), true) ),
							    `VSpacing (0.6),
							    `Left(`RadioButton(`id(`scsi),`opt(`hstretch),
									       _("&SCSI scanner"),  false) ),
							    `VSpacing (1.2),
							    `Left(`RadioButton(`id(`net),`opt(`hstretch),
									       _("&Network scan station") ,  false) ),
							    `VSpacing (0.6)
							    )
						      )
				    ),
			    `HSpacing(3.0)
			    ),
		      `VSpacing(1.0)
		      );
      return layout;
  }

/**
 * ScannerStartDialog
 */
  global define ScannerNotFoundDialog()``{
      string help = "";
      string help1    = UI(_("<p>Please check first whether your scanner is
connected correctly and is switched on.</p> "));
      string help2    = UI(_("<p>In case of USB you can connect and switch
on the scanner before going on.<br>
<i><b>Attention!</b></i> Do NOT connect or disconnect a SCSI scanner
on running system - but <b>Abort</b> scanner installation, shutdown
and reboot. 
</p>"));
      string help3    = UI(_("<p>YaST2 provides assistence for manual
USB and SCSI scanner configuration. You also can use
a scanner at a network scan station. Select the correct button
and click on <b>Next</b>.</p>"));
       string help4    = UI(_("<p>Please note: only <i>one</i> USB scanner
is supported.</p>"));

      help = help1 + help2 + help3 + help4;
      
      string caption = UI(_("Manual scanner configuration"));
      term contents  = ScannerNotFoundDialog_UI();

      UI(`SetWizardContentsButtons(caption,contents,help,BackButtonLabel(),NextButtonLabel()));

      any ret = nil;
      boolean go_on = true;

      while ( go_on )
      {
	  go_on = true;
	  
	  ret = UI(`UserInput());

	  if ( ret == `next || ret == `abort || ret == `back )
	  {
	      go_on = false;
	  }
	  
	  if ( ret == `next )
	  {
	      symbol type = UI(`QueryWidget(`id(`scanner), `CurrentButton));
	      y2debug("TYPE: %1", type );

	      if ( type == `usb )
	      {
                  // try again to detect a USB scanner ( maybe it's switched on now)
		  list connected_scanner = SCR(`Read(.probe.scanner));

		  if ( size (connected_scanner) > 1 )
		  {
		      // text of a warning popup
		      UI(`WarningPopup(_("YaST2 has detected several USB scanner -
but only ONE is supported.
Please disconnect the unwanted USB scanner(s)
and restart the Scanner configuration.")));
		      go_on = false;
		      ret = `abort;
		  }
		  else if ( size (connected_scanner) == 1 )
		  {
		      // start again with scanner start dialog
		      // TODO ? sollte immer funktionieren ?
		      //    -> scanner jetzt eingesteckt/aber bereits installiert -> overview
		      //	-> scanner neu, scanner nicht unterstützt -> manual
		      //	-> scanner neu und unterstützt -> new
		      ret = `start;
		  }
		  else
		  {
		      boolean usb_loaded = true;
		      
		      // TODO: make sure module scanner is not yet loaded -> lsmod
		      //       ONLY go to dialog load USB module if NOT yet loaded

		      if ( usb_loaded )
		      {
			  ret = `usb;
		      }
		      else
		      {
                          // USB module is already loaded -> go to manual scanner selection
			  ret = `manual;
		      }
		      // create the entry for the USB scanner
		      CreateScannerEntry( $["bus":"USB", "devicefile":"/dev/usbscanner"]);
		      
		  }
	      }
	      else
	      {
		  ret = type;
	      }
	  }
      };

      return ret;
  }

}
